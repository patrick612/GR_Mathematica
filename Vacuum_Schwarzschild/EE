InverseMetric[g_] := Simplify[Inverse[g]]

ChristoffelSymbol[g_, xx_] := 
 Block[{n, ig, res}, n = Length[g]; ig = InverseMetric[g];
  res = Table[(1/2)*
     Sum[ig[[i, 
        s]]*(-D[g[[j, k]], xx[[s]]] + D[g[[j, s]], xx[[k]]] + 
         D[g[[s, k]], xx[[j]]]), {s, 1, n}], {i, 1, n}, {j, 1, n}, {k,
      1, n}];
  Simplify[res]]

RiemannTensor[g_, xx_] := 
 Block[{n, Chr, res}, n = Length[g]; Chr = ChristoffelSymbol[g, xx];
  res = Table[
    D[Chr[[i, k, m]], xx[[l]]] - D[Chr[[i, k, l]], xx[[m]]] + 
     Sum[Chr[[i, s, l]]*Chr[[s, k, m]], {s, 1, n}] - 
     Sum[Chr[[i, s, m]]*Chr[[s, k, l]], {s, 1, n}], {i, 1, n}, {k, 1, 
     n}, {l, 1, n}, {m, 1, n}];
  Simplify[res]]

RicciTensor[g_, xx_] := 
 Block[{Rie, res, n}, n = Length[g]; Rie = RiemannTensor[g, xx];
  res = Table[Sum[Rie[[s, i, s, j]], {s, 1, n}], {i, 1, n}, {j, 1, n}];
  Simplify[res]]

RicciScalar[g_, xx_] := 
 Block[{Ricc, ig, res, n}, n = Length[g]; Ricc = RicciTensor[g, xx]; 
  ig = InverseMetric[g];
  res = Sum[ig[[s, i]] Ricc[[s, i]], {s, 1, n}, {i, 1, n}];
  Simplify[res]]

g1 = {{b[r], 0, 0, 0}, {0, a[r], 0, 0}, {0, 0, r^2, 0}, {0, 0, 0, 
    r^2 Sin[theta]^2}};
derivlist = {t, r, theta, phi};
chris = ChristoffelSymbol[g1, derivlist];
ricciT = RicciTensor[g1, derivlist];

ricciT

FullSimplify[ricciT[[3]][[3]] == ricciT[[2]][[2]]*Sin[theta]^2]

radialde = 
 Solve[{ricciT[[1]][[1]] == 0, ricciT[[2]][[2]] == 0, 
   ricciT[[3]][[3]] == 0, ricciT[[4]][[4]] == 0}, {a[r], D[a[r], r], 
   b[r], D[b[r], r], D[D[b[r], r], r] }]

radialde1[r_] = D[a[r], r] /. radialde[[1]][[1]]

DSolve[D[a[r], r] == radialde1[r], a[r], r]

a[r] = a[r] /. DSolve[D[a[r], r] == radialde1[r], a[r], r]

a[r] = a[r][[1]]

radialde2[r_] = D[b[r], r] /. radialde[[1]][[2]]

DSolve[D[b[r], r] == radialde2[r], b[r], r]

b[r] = b[r] /. DSolve[D[b[r], r] == radialde2[r], b[r], r]

b[r] = b[r][[1]]
